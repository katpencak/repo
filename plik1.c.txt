// Wyznacznik przez rozk³ad LU
// Data: 10.02.2011
// (C)2012 mgr Jerzy Wa³aszek
//-----------------------------

program ludet;

type
  NReal = array of double;  // typ tablicy wierszy
  MReal = array of NReal;   // typ tablicy wskaŸników

const

  eps = 1e-12;

// Funkcja realizuje algorytm rozk³adu LU
//---------------------------------------
function lu(n : integer; A : MReal) : boolean;

var
  i,j,k : integer;

begin

  for k := 0 to n - 2 do
  begin
    if abs(A[k][k]) < eps then Exit(false);
    for i := k + 1 to n - 1 do
      A[i][k] := A[i][k] / A[k][k];
    for i := k + 1 to n - 1 do
      for j := k + 1 to n - 1 do
        A[i][j] := A[i][j] - A[i][k] * A[k][j];
  end;
  lu := true;
end;


// Program g³ówny

var
  A     : MReal;
  n,i,j : integer;
  det   : double;

begin

  // odczytujemy stopieñ macierzy A

  read(n);

  // tworzymy macierz A o odpowiednich rozmiarach

  SetLength(A,n);

  for i := 0 to n - 1 do SetLength(A[i],n);

  // odczytujemy dane dla macierzy A

  for i := 0 to n - 1 do
    for j := 0 to n - 1 do read(A[i][j]);

  // obliczamy wyznacznik

  if lu(n,A) then
  begin
    det := A[0][0];
    for i := 1 to n - 1 do det := det * A[i][i];
    writeln(det:0:4);
  end
  else
    writeln('DZIELNIK ZERO');

  // usuwamy macierz z pamiêci

  for i := 0 to n - 1 do SetLength(A[i],0);
  SetLength(A,0);

end. 